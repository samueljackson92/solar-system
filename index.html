<html>
    <head>
        <!-- Default CSS style -->
        <link rel="stylesheet" type="text/css" href="css/style.css">
        <!-- End of CSS imports -->

        <!-- WebGl shader imports -->
        <script id="basic-fs" type="x-shader/x-fragment">
            precision mediump float;

            varying vec2 vTextureCoord;
            varying vec3 vTransformedNormal;
            varying vec4 vPosition;

            varying vec3 vCoords;

            uniform vec3 uAmbientColor;
            uniform samplerCube uSampler;

            void main(void) {
                vec4 fragmentColor = textureCube(uSampler, vCoords);
                gl_FragColor = vec4(fragmentColor.rgb * uAmbientColor, fragmentColor.a);
            }
        </script>
        <script id="basic-vs" type="x-shader/x-vertex">
            attribute vec3 aVertexPosition;
            attribute vec3 aVertexNormal;
            attribute vec2 aTextureCoord;

            uniform mat4 uMVMatrix;
            uniform mat4 uPMatrix;
            uniform mat3 uNMatrix;

            varying vec2 vTextureCoord;
            varying vec3 vTransformedNormal;
            varying vec4 vPosition;

            varying vec3 vCoords;

            void main(void) {
                vPosition = uMVMatrix * vec4(aVertexPosition, 1.0);
                gl_Position = uPMatrix * vPosition;
                vTextureCoord = aTextureCoord;
                vTransformedNormal = uNMatrix * aVertexNormal;
                vCoords = vPosition.xyz;
            }
        </script>

        <!-- fragment shader -->
        <script id="shader-fs" type="x-shader/x-fragment">
            precision mediump float;

            varying vec2 vTextureCoord;
            varying vec3 vTransformedNormal;
            varying vec4 vPosition;

            //lighting uniforms
            uniform vec3 uAmbientColor;
            uniform vec3 uEmissiveColor;

            //light attenuation parameters
            uniform float uConstantLightAttenuation;
            uniform float uLinearLightAttenuation;
            uniform float uQuadraticLightAttenuation;
            uniform float uAtmosphereRotation;

            //point light location and colour uniforms
            uniform vec3 uPointLightingLocation;
            uniform vec3 uPointLightingColor;

            uniform float uMaterialShininess;
            uniform bool uNoDirectionalLight;

            //texture sampler uniforms
            uniform sampler2D uSampler;
            uniform bool useNightTexture;
            uniform sampler2D uSamplerDark;
            uniform bool useAtmosphere;
            uniform sampler2D uSamplerAtmosphere;

            //alpha weight for textures
            uniform float uAlpha;

            //calculate light attenuation from a light source
            float calculateLightAttentuation(float r)
            {
                return min(1.0 / (uConstantLightAttenuation + uLinearLightAttenuation * r + uQuadraticLightAttenuation * r*r), uMaterialShininess);
            }

            // Sample from and rotate the atmosphere texture
            vec4 textureAtmosphere(vec4 fragmentColor)
            {
                if (useAtmosphere)
                {
                    vec2 rotatedAtmosphere = vec2(vTextureCoord.x + uAtmosphereRotation, vTextureCoord.y);
                    vec4 atmosphere = texture2D(uSamplerAtmosphere, vec2(rotatedAtmosphere.s, rotatedAtmosphere.t));
                    fragmentColor = vec4(atmosphere.a) * atmosphere + vec4(1.0 - atmosphere.a) * fragmentColor;
                }

                return fragmentColor;
            }

            // Texture the dark side of the planet with interpolation between the two sides
            vec4 textureDarkSideOfPlanet(vec4 fragmentColor, float diffuseLightWeighting)
            {
                if(useNightTexture)
                {
                    vec4 lightColor = texture2D(uSampler, vec2(vTextureCoord.s, vTextureCoord.t));
                    vec4 darkColor = texture2D(uSamplerDark, vec2(vTextureCoord.s, vTextureCoord.t));
                    fragmentColor.xyz = (lightColor.xyz * diffuseLightWeighting + darkColor.xyz * (1.0-diffuseLightWeighting));
                }

                return fragmentColor;
            }

            void main(void) {
                vec3 lightWeighting;

                vec4 fragmentColor = texture2D(uSampler, vec2(vTextureCoord.s, vTextureCoord.t));

                if (uNoDirectionalLight) {
                    lightWeighting = uAmbientColor;
                } else {
                    vec3 normal = normalize(vTransformedNormal);
                    vec3 eyeDirection = normalize(-vPosition.xyz);
                    vec3 lightDirection = uPointLightingLocation - vPosition.xyz;
                    vec3 lightNormal = normalize(lightDirection);
                    vec3 reflectionDirection = reflect(-lightNormal, normal);

                    float lightAttenuation = calculateLightAttentuation(length(lightDirection));
                    float diffuseLightWeighting = max(dot(normal, lightNormal), 0.0);
                    float specularLightWeighting = pow(max(dot(reflectionDirection, eyeDirection), 0.0), uMaterialShininess);

                    //correction for the loss of angle information during reflection of the normal
                    specularLightWeighting *= diffuseLightWeighting;
                    lightWeighting = uEmissiveColor + lightAttenuation * uPointLightingColor * (uAmbientColor + specularLightWeighting + diffuseLightWeighting);

                    fragmentColor = textureDarkSideOfPlanet(fragmentColor, diffuseLightWeighting);
                }

                fragmentColor = textureAtmosphere(fragmentColor);
                gl_FragColor = vec4(fragmentColor.rgb * lightWeighting, fragmentColor.a * uAlpha);
            }
        </script>

        <!-- vertex shader -->
        <script id="shader-vs" type="x-shader/x-vertex">
            attribute vec3 aVertexPosition;
            attribute vec3 aVertexNormal;
            attribute vec2 aTextureCoord;

            uniform mat4 uMVMatrix;
            uniform mat4 uPMatrix;
            uniform mat3 uNMatrix;

            varying vec2 vTextureCoord;
            varying vec3 vTransformedNormal;
            varying vec4 vPosition;

            void main(void) {
                vPosition = uMVMatrix * vec4(aVertexPosition, 1.0);
                gl_Position = uPMatrix * vPosition;
                vTextureCoord = aTextureCoord;
                vTransformedNormal = uNMatrix * aVertexNormal;
            }
        </script>
        <!-- End of WebGL shaders -->

        <!-- Javascript imports -->
        <!-- library includes -->
        <script src="js/third_party/gl-matrix-min.js" type="text/javascript"></script>
        <script src="js/third_party/webgl-utils.js" type="text/javascript"></script>
        <script src="js/third_party/classie.js" type="text/javascript"></script>
        <!-- application includes -->
        <script src="js/utils.js" type="text/javascript"></script>
        <script src="js/models/basic_models.js" type="text/javascript"></script>
        <script src="js/models/celestial_body.js" type="text/javascript"></script>
        <script src="js/models/sky_box.js" type="text/javascript"></script>
        <script src="js/models/rings.js" type="text/javascript"></script>
        <script src="js/scene_graph.js" type="text/javascript"></script>
        <script src="js/shader_programs.js" type="text/javascript"></script>
        <script src="js/key_controller.js" type="text/javascript"></script>
        <script src="js/camera.js" type="text/javascript"></script>
        <script src="js/texture_loader.js" type="text/javascript"></script>
        <script src="js/main.js" type="text/javascript"></script>
        <!-- End of Javascript imports -->

    </head>
    <body onLoad="webGlStart();">
        <nav class="menu push-menu-right">
            <ul>
                <li><h3 class="menu-section-header">Keyboard Controls</h3></li>
                <li>
                    <p>Q - Open/Close menu</p>
                    <p>W - Zoom In</p>
                    <p>S - Zoom Out</p>
                    <p>Up/Down/Left/Right - Rotate Camera</p>
                    <p>0-9 - Select Planet</p>
                </li>
                <li><h3 class="menu-section-header">Lighting Parameters</h3></li>
                <li><p>Directional:<input type="range" class="menu-input-range" id="directional-light-intensity" value="50" max="100", min="0" /></p></li>
                <li><p>Ambient: <input type="range" class="menu-input-range" id="ambient-light-intensity" value="50" max="100", min="0" /></p></li>
                <li><p>Shininess: <input type="range" class="menu-input-range" id="light-shininess" value="3" max="6", min="0" /></p></li>
                <li><h4>Light Attenuation</h4></li>
                <li><p>Constant: <input type="text" class="menu-input-single" id="light-attenuation-constant" value="0.0001" /></p></li>
                <li><p>Linear: <input type="text" class="menu-input-single" id="light-attenuation-linear" value="0.0001" /></p></li>
                <li><p>Quadratic: <input type="text" class="menu-input-single" id="light-attenuation-quadratic" value="0.0001" /></p></li>
            </ul>
        </nav><!-- /push menu right -->
         <div id="wrapper">
        <div id="main">
            <div class="container">
                <canvas id="canvas" style="border: none;" width="500" height="500"></canvas>
            </div>
        </div>
    </body>
    <script type="text/javascript">
    (function( window ){
        var body = document.body,
            mask = document.createElement("div"),
            togglePushRight = document.querySelector( ".toggle-push-right" ),
            pushMenuRight = document.querySelector( ".push-menu-right" ),
            activeNav = "";

        document.addEventListener( "keydown", function(e){
            if(e.keyCode === Keys.Q_KEY && activeNav === "")
            {
                classie.add( document.body, "pmr-open" );
                activeNav = "pmr-open";
            }
            else if (e.keyCode === Keys.Q_KEY && activeNav !== "")
            {
                classie.remove( document.body, activeNav );
                activeNav = "";
            }
        } );

    })( window );
    </script>
</html>
