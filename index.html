<html>
    <head>
        <!-- Default CSS style -->
        <link rel="stylesheet" type="text/css" href="css/style.css">
        <!-- End of CSS imports -->

        <!-- WebGl shader imports -->
        <script id="basic-fs" type="x-shader/x-fragment">
            precision mediump float;

            varying vec2 vTextureCoord;
            varying vec3 vTransformedNormal;
            varying vec4 vPosition;

            varying vec3 vCoords;

            uniform vec3 uAmbientColor;
            uniform samplerCube uSampler;

            void main(void) {
                vec4 fragmentColor = textureCube(uSampler, vCoords);
                gl_FragColor = vec4(fragmentColor.rgb * uAmbientColor, fragmentColor.a);
            }
        </script>
        <script id="basic-vs" type="x-shader/x-vertex">
            attribute vec3 aVertexPosition;
            attribute vec3 aVertexNormal;
            attribute vec2 aTextureCoord;

            uniform mat4 uMVMatrix;
            uniform mat4 uPMatrix;
            uniform mat3 uNMatrix;

            varying vec2 vTextureCoord;
            varying vec3 vTransformedNormal;
            varying vec4 vPosition;

            varying vec3 vCoords;

            void main(void) {
                vPosition = uMVMatrix * vec4(aVertexPosition, 1.0);
                gl_Position = uPMatrix * vPosition;
                vTextureCoord = aTextureCoord;
                vTransformedNormal = uNMatrix * aVertexNormal;
                vCoords = vPosition.xyz;
            }
        </script>

        <!-- fragment shader -->
        <script id="shader-fs" type="x-shader/x-fragment">
            precision mediump float;

            varying vec2 vTextureCoord;
            varying vec3 vTransformedNormal;
            varying vec4 vPosition;

            //lighting uniforms
            uniform vec3 uAmbientColor;
            uniform vec3 uEmissiveColor;
            uniform float uAlpha;

            uniform float uConstantLightAttenuation;
            uniform float uLinearLightAttenuation;
            uniform float uQuadraticLightAttenuation;
            uniform float uAtomosphereRotation;

            uniform vec3 uPointLightingLocation;
            uniform vec3 uPointLightingColor;

            uniform float uMaterialShininess;
            uniform bool uNoDirectionalLight;

            uniform sampler2D uSampler;
            uniform bool useNightTexture;
            uniform sampler2D uSamplerDark;
            uniform bool useAtomosphere;
            uniform sampler2D uSamplerAtomosphere;

            //calculate light attenuation from a light source
            float calculateLightAttentuation(float r)
            {
                return min(1.0 / (uConstantLightAttenuation + uLinearLightAttenuation * r + uQuadraticLightAttenuation * r*r), uMaterialShininess);
            }

            void main(void) {
                vec3 lightWeighting;

                vec4 fragmentColor = texture2D(uSampler, vec2(vTextureCoord.s, vTextureCoord.t));

                if (uNoDirectionalLight) {
                    lightWeighting = uAmbientColor;
                } else {
                    vec3 normal = normalize(vTransformedNormal);
                    vec3 eyeDirection = normalize(-vPosition.xyz);
                    vec3 lightDirection = uPointLightingLocation - vPosition.xyz;
                    vec3 lightNormal = normalize(lightDirection);
                    vec3 reflectionDirection = reflect(-lightNormal, normal);

                    float lightAttenuation = calculateLightAttentuation(length(lightDirection));
                    float diffuseLightWeighting = max(dot(normal, lightNormal), 0.0);
                    float specularLightWeighting = pow(max(dot(reflectionDirection, eyeDirection), 0.0), uMaterialShininess);
                    //correction for the loss of angle information during reflection of the normal
                    specularLightWeighting *= diffuseLightWeighting;

                    lightWeighting = uEmissiveColor + lightAttenuation * uPointLightingColor * (uAmbientColor + specularLightWeighting + diffuseLightWeighting);

                    if(useNightTexture)
                    {
                        vec4 lightColor = texture2D(uSampler, vec2(vTextureCoord.s, vTextureCoord.t));
                        vec4 darkColor = texture2D(uSamplerDark, vec2(vTextureCoord.s, vTextureCoord.t));

                        fragmentColor.x = (lightColor.x * diffuseLightWeighting + darkColor.x * (1.0-diffuseLightWeighting));
                        fragmentColor.y = (lightColor.y * diffuseLightWeighting + darkColor.y * (1.0-diffuseLightWeighting));
                        fragmentColor.z = (lightColor.z * diffuseLightWeighting + darkColor.z * (1.0-diffuseLightWeighting));
                    }
                }

                if (useAtomosphere)
                {
                    vec2 tex = vec2(vTextureCoord.x + uAtomosphereRotation, vTextureCoord.y);
                    vec4 atomosphere = texture2D(uSamplerAtomosphere, vec2(tex.s, tex.t));

                    gl_FragColor = vec4(atomosphere.a) * atomosphere + vec4(1.0 - atomosphere.a) * fragmentColor;
                    gl_FragColor = vec4(gl_FragColor.rgb * lightWeighting, gl_FragColor.a * uAlpha);
                }
                else
                {
                    gl_FragColor = vec4(fragmentColor.rgb * lightWeighting, fragmentColor.a * uAlpha);
                }
            }
        </script>

        <!-- vertex shader -->
        <script id="shader-vs" type="x-shader/x-vertex">
            attribute vec3 aVertexPosition;
            attribute vec3 aVertexNormal;
            attribute vec2 aTextureCoord;

            uniform mat4 uMVMatrix;
            uniform mat4 uPMatrix;
            uniform mat3 uNMatrix;

            varying vec2 vTextureCoord;
            varying vec3 vTransformedNormal;
            varying vec4 vPosition;

            void main(void) {
                vPosition = uMVMatrix * vec4(aVertexPosition, 1.0);
                gl_Position = uPMatrix * vPosition;
                vTextureCoord = aTextureCoord;
                vTransformedNormal = uNMatrix * aVertexNormal;
            }
        </script>
        <!-- End of WebGL shaders -->

        <!-- Javascript imports -->
        <!-- library includes -->
        <script src="js/third_party/gl-matrix-min.js" type="text/javascript"></script>
        <script src="js/third_party/webgl-utils.js" type="text/javascript"></script>
        <!-- application includes -->
        <script src="js/utils.js" type="text/javascript"></script>
        <script src="js/models/basic_models.js" type="text/javascript"></script>
        <script src="js/models/celestial_body.js" type="text/javascript"></script>
        <script src="js/models/sky_box.js" type="text/javascript"></script>
        <script src="js/models/rings.js" type="text/javascript"></script>
        <script src="js/scene_graph.js" type="text/javascript"></script>
        <script src="js/shader_programs.js" type="text/javascript"></script>
        <script src="js/camera.js" type="text/javascript"></script>
        <script src="js/texture_loader.js" type="text/javascript"></script>
        <script src="js/key_controller.js" type="text/javascript"></script>
        <script src="js/main.js" type="text/javascript"></script>
        <!-- End of Javascript imports -->
    </head>
    <body onLoad="webGlStart();">
        <canvas id="canvas" style="border: none;" width="500" height="500"></canvas>
    </body>
</html>
